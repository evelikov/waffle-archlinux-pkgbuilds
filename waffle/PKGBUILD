# Maintainer: Chad Versace <chad.versace@linux.intel.com>

pkgname='waffle'
pkgver='1.5.0'
pkgrel='1'
pkgdesc='a library for choosing window system and OpenGL API at runtime'
arch=('i686' 'x86_64')
url='http://www.waffle-gl.org'
license=('BSD')

depends=(
  # As of 1.5.0, libwaffle still links to the libraries below.
  'libx11'
  'libxcb'
  'wayland'
  )
makedepends=(
  'cmake'
  'xcb-proto'

  # As of 1.5.0, the libraries below are not runtime dependencies because
  # libwaffle dynamically loads them. However, they're headers are required at
  # buildtime.
  'libegl'
  'libgbm'
  'libgl'


  # For building the docs.
  'libxslt'
  'docbook-xsl'
  )
source=("http://www.waffle-gl.org/files/release/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('6344fa2803d76182a08380780605b36c38e795639a3229281c96b75cc30137a8')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Dwaffle_has_gbm=1 \
    -Dwaffle_has_glx=1 \
    -Dwaffle_has_x11_egl=1 \
    -Dwaffle_has_wayland=1 \
    -Dwaffle_build_manpages=1 \
    -Dwaffle_build_htmldocs=1 \
    -Dwaffle_build_examples=0
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$pkgdir/usr/share/doc/waffle1/LICENSE.txt" \
                "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
